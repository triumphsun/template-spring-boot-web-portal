plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springManagementVersion}"
    id 'java'
    id 'idea'
}

group = "com.suntri.${rootProject.name}"
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    //production
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${juitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${juitJupiterVersion}"

    //integration tests
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    integrationTestImplementation "org.junit.jupiter:junit-jupiter-api:${juitJupiterVersion}"
    integrationTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${juitJupiterVersion}"
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = LifecycleBasePlugin.VERIFICATION_GROUP

    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.allJava.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
    }
}